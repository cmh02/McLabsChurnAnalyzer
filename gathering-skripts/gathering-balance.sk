###
Script: skGathering - Balance Skript
Author: cmh02
Version: 1.0

This script will collect the balances of all players that have joined the server.
###

# Skript-Wide Options
options:
	labsprefix: <##00cdf6>&lMC<##00cdf6>&lL<##00b8e8>&la<##00a2da>&lb<##008dcc>&ls &fÂ»

# Load-Up Events
on script load:
	# Log the script load in console
	skGathering_messageLogFile("The Gathering Balance Skript has been loaded on server %{server}%!")

#######################
# GATHERING FUNCTIONS #
#######################

function skGathering_writeBalancesToFile(outputFileName: string):

	# Make sure the list of UUID's has been loaded
	if {skgathering::uuids::*} is not set:
		skGathering_messageLogFile("A call was made to get all player balance data, but UUID's have not been loaded yet!", false, "SEVERE")
		stop

	# Prepare data output list with headers
	clear {_balanceOutputData::*}
	add "UUID, balance" to {_balanceOutputData::*}

	# Loop over UUID's to get balances
	loop {skgathering::uuids::*}:

		# Get the player's balance
		set {_player} to offlineplayer(loop-value)
		set {_balance} to "%{_player}'s balance%"
		replace every "," with "" in {_balance}
		replace every "$" with "" in {_balance}

		# Prepare data in CSV format
		add "%loop-value%,%{_balance}%" to {_balanceOutputData::*}

	# Write all data to output file
	skGathering_messageOutputFile(outputFileName, {_balanceOutputData::*})