###
Script: skGathering - LabWars Skript
Author: cmh02
Version: 1.0

This script will collect the LabWars (a periodic competitive server event) data of all players that have joined the server.
###

# Skript-Wide Options
options:
	labsprefix: <##00cdf6>&lMC<##00cdf6>&lL<##00b8e8>&la<##00a2da>&lb<##008dcc>&ls &fÂ»

# Load-Up Events
on script load:
	# Log the script load in console
	skGathering_messageLogFile("The Gathering LabWars Skript has been loaded on server %{server}%!")

	# Load labwars mongo collection
	set {mongosk::collection::labwars} to mongo collection named "labwars13" of {mongosk::event}

	# Make section for querying data in async
	create new section with {_uuid} stored in {skgathering::asyncs::getLabWarsDataForUuid}:

		# Get the player's documents from Mongo database
		set {_player} to offlineplayer({_uuid})
		set {_labWarsMongoDocument} to first mongo document with mongosk filter where field "_id" is "%{_uuid}%" of collection {mongosk::collection::labwars}
		set {_revenueMongoDocument} to mongo value "revenue" of {_labWarsMongoDocument}
		set {_revTotalMongoDocument} to mongo value "total" of {_revenueMongoDocument}
		set {_revPhaseMongoDocument} to mongo value "week" of {_revenueMongoDocument}

		# Get total and phase rev amounts
		set {_revTotal} to mongo value "all" of {_revTotalMongoDocument}
		set {_revPhase} to mongo value "all" of {_revPhaseMongoDocument}

		# Default values
		if {_revTotal} is not set:
			set {_revTotal} to 0
		if {_revPhase} is not set:
			set {_revPhase} to 0

		# Prepare data in CSV format
		return "%{_revTotal}%,%{_revPhase}%"

#######################
# GATHERING FUNCTIONS #
#######################

function skGathering_writeLabWarsToFile(outputFileName: string, outputFileTime: string):

	# Make sure the list of UUID's has been loaded
	if {skgathering::uuids::*} is not set:
		skGathering_messageLogFile("A call was made to get all player LabWars data, but UUID's have not been loaded yet!", false, "SEVERE")
		stop

	# Prepare data output list with headers
	clear {_labWarsOutputData::*}
	add "UUID, lw_rev_total, lw_rev_phase" to {_labWarsOutputData::*}

	# Loop over UUID's to get votes
	loop {skgathering::uuids::*}:

		# Check if estop is activated
		if {skgathering::estop} is true:
			skGathering_messageLogFile("E-Stop activated! Aborting LabWars gathering operation.", false, "WARNING")
			stop

		# Get the player's labwars data
		run section {skgathering::asyncs::getLabWarsDataForUuid} async with arguments loop-value and store result in {_result} and wait

		# Prepare data in CSV format
		add "%loop-value%,%{_result}%" to {_labWarsOutputData::*}

	# Write all data to output file
	skGathering_messageOutputFile("%{_outputFileName}%", "%{_outputFileTime}%", {_labWarsOutputData::*})